plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    forge {
        mixinConfig "artifactory.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    modRuntimeOnly "maven.modrinth:artifacts:$rootProject.artifacts_forge_version"
    modRuntimeOnly("be.florens:expandability-forge:$rootProject.expandability_version")
    modRuntimeOnly("me.shedaniel.cloth:cloth-config-forge:$rootProject.cloth_config_version")
    modRuntimeOnly("top.theillusivec4.curios:curios-forge:$rootProject.curios_version")

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:$rootProject.mixin_extras_version"))
    implementation(include("io.github.llamalad7:mixinextras-forge:$rootProject.mixin_extras_version"))

    modRuntimeOnly("curse.maven:cosmetic-armor-reworked-237307:4600191")
    modRuntimeOnly("maven.modrinth:curious-armor-stands:1.20-5.0.1")

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
